plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
    id "io.freefair.lombok" version "8.4" apply false
    id "org.sonarqube" version "4.4.1.3373"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply {
        plugin("dev.architectury.loom")
        plugin("io.freefair.lombok")
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        //mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:@YARN_MAPPINGS@:v2"
        mappings loom.layered() {
            officialMojangMappings()
            //parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
            parchment("org.parchmentmc.data:parchment-1.20.2:${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply {
        plugin("java")
        plugin("architectury-plugin")
        plugin("maven-publish")
    }

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }

        exclusiveContent {
            forRepository {
                maven {
                    name = "Modrinth"
                    url = "https://api.modrinth.com/maven"
                }
            }

            filter {
                includeGroup "maven.modrinth"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release.set(17)
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
//            maven {
//                name = "ManasCore"
//                url "https://api-g.cloudsmith.io/maven/manasmods/manascore/"
//                credentials {
//                    username = project.findProperty("cloudsmith_user") ?: System.getenv("CLOUDSMITH_USER")
//                    password = project.findProperty("cloudsmith_password") ?: System.getenv("CLOUDSMITH_PASSWORD")
//                }
//            }
            maven {
                url "file://${rootProject.projectDir}/mcmodsrepo"
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "ManasMods_ManasCore"
        property "sonar.organization", "manasmods"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
